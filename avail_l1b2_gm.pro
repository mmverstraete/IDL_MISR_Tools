PRO avail_l1b2_gm

   ;Sec-Doc
   ;  PURPOSE: This program saves the information on the availability of
   ;  MISR L1B2 data files on the current computer at the time of
   ;  execution.
   ;
   ;  ALGORITHM: This program relies on the function avail_l1b2_gm_data to
   ;  search the standard locations for MISR L1B2 Georectified Radiance
   ;  Product (GRP) Terrain-Projected Top of Atmosphere (ToA) Global Mode
   ;  (GM) Radiance files on this computer and to provide information on
   ;  their availability. This program saves the outcome in a log file.
   ;  Input and output files are assumed to be in the standard locations
   ;  defined by the function set_misr_roots.pro.
   ;
   ;  SYNTAX: avail_l1b2_gm
   ;
   ;  POSITIONAL PARAMETERS [INPUT/OUTPUT]: None.
   ;
   ;  KEYWORD PARAMETERS [INPUT/OUTPUT]: None.
   ;
   ;  RETURNED VALUE TYPE: N/A.
   ;
   ;  OUTCOME:
   ;
   ;  *   If no exception condition has been detected, this program
   ;      gathers and saves basic information on the availability of MISR
   ;      L1B2 files in the folders of misr_roots[0] at the time of
   ;      execution in an output file named
   ;      MISR_L1B2_GM_Data_Avail_cccc_YYYY-MM-DD_hh-mm-ss.txt
   ;      where cccc is the hostname of the computer, and the rest of the
   ;      file name up to the file extension indicates the date and time
   ;      when this information was gathered; this file is located in the
   ;      folder located at
   ;      misr_roots[2] + ’/Available/L1B2_GM_data/’
   ;      where misr_roots[0] and misr_roots[2] are defined by the
   ;      function
   ;      set_misr_roots.pro.
   ;
   ;  *   If an exception condition has been detected, this program prints
   ;      out an error message; no file is saved.
   ;
   ;  EXCEPTION CONDITIONS:
   ;
   ;  *   Error 200: An exception condition occurred in function
   ;      avail_l1b2_gm_data.pro.
   ;
   ;  DEPENDENCIES:
   ;
   ;  *   avail_l1b2_gm_data.pro
   ;
   ;  *   set_misr_roots.pro
   ;
   ;  *   strstr.pro
   ;
   ;  *   today.pro
   ;
   ;  REMARKS:
   ;
   ;  *   NOTE 1: This program currently reports on the first and last
   ;      MISR L1B2 data files found in standard places on this computer.
   ;      It does not verify the availability of all 9 L1B2 files per
   ;      ORBIT, or of the associated Geometric Parameters and L2 Land
   ;      Surface files, which are also required by the MISR-HR processing
   ;      system. Nor does it report on the version status of the data
   ;      files.
   ;
   ;  EXAMPLES:
   ;
   ;      IDL> avail_l1b2_gm
   ;      The output file
   ;      MISR_L1B2_GM_Data_Avail_micmac_2017-11-12_23-32-39.txt
   ;      has been created in the folder
   ;      ~/Documents/MISR_HR/Outcomes/Available/L1B2_data
   ;
   ;      IDL> SPAWN, 'cat ~/Documents/MISR_HR/Outcomes/Available/L1B2_data/
   ;         MISR_L1B2_GM_Data_Avail_micmac_2017-11-12_23-32-39.txt'
   ;      File name: MISR_L1B2_GM_Data_Avail_micmac_2017-11-12_23-32-39.txt
   ;      File path: /Users/mmverstraete/Documents/MISR_HR/Outcomes/
   ;         Available/L1B2_data/
   ;
   ;         Generated by: AVAIL_L1B2_GM
   ;             Saved on: 2017-11-12_23-32-39
   ;
   ;              Content: MISR L1B2 data availability on computer micmac.
   ;
   ;      /Volumes/MISR_Data4/P196/ 466G from: 2000-02-25 to: 2017-02-23
   ;      /Volumes/MISR_Data4/P197/ 475G from: 2000-03-03 to: 2017-02-14
   ;      /Volumes/MISR_Data4/P198/ 480G from: 2000-03-10 to: 2017-02-21
   ;      /Volumes/MISR_Data4/P199/ 479G from: 2000-03-01 to: 2017-02-28
   ;
   ;  REFERENCES: None.
   ;
   ;  VERSIONING:
   ;
   ;  *   2017–07–24: Version 0.9 — Initial release.
   ;
   ;  *   2017–11–30: Version 1.0 — Initial public release.
   ;
   ;
   ;Sec-Lic
   ;  INTELLECTUAL PROPERTY RIGHTS
   ;
   ;  *   Copyright (C) 2017 Michel M. Verstraete.
   ;
   ;      Permission is hereby granted, free of charge, to any person
   ;      obtaining a copy of this software and associated documentation
   ;      files (the “Software”), to deal in the Software without
   ;      restriction, including without limitation the rights to use,
   ;      copy, modify, merge, publish, distribute, sublicense, and/or
   ;      sell copies of the Software, and to permit persons to whom the
   ;      Software is furnished to do so, subject to the following
   ;      conditions:
   ;
   ;      The above copyright notice and this permission notice shall be
   ;      included in all copies or substantial portions of the Software.
   ;
   ;      THE SOFTWARE IS PROVIDED “AS IS”, WITHOUT WARRANTY OF ANY KIND,
   ;      EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
   ;      OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
   ;      NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
   ;      HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
   ;      WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
   ;      FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
   ;      OTHER DEALINGS IN THE SOFTWARE.
   ;
   ;      See: https://opensource.org/licenses/MIT.
   ;
   ;  *   Feedback
   ;
   ;      Please send comments and suggestions to the author at
   ;      MMVerstraete@gmail.com.
   ;
   ;
   ;Sec-Cod
   ;  Define the standard locations for the MISR and MISR-HR files on this
   ;  computer:
   misr_roots = set_misr_roots()
   m_dir = misr_roots[0] + 'P*/'
   a_dir = misr_roots[2] + 'Available/L1B2_data/'
   IF (is_writable(a_dir) NE 1) THEN BEGIN
      FILE_MKDIR, a_dir
   ENDIF

   ;  Get the information on MISR L1B2 data files availability:
   rc = avail_l1b2_gm_data(m_dir, misr_data, EXCPT_COND = excpt_cond)
   IF (rc NE 0) THEN BEGIN
      info = SCOPE_TRACEBACK(/STRUCTURE)
      rout_name = info[N_ELEMENTS(info) - 1].ROUTINE
      error_code = 200
      excpt_cond = 'Error ' + strstr(error_code) + ' in ' + rout_name + $
         ': ' + excpt_cond
      PRINT, excpt_cond
      RETURN
   ENDIF

   ;  Identify the current computer:
   SPAWN, 'hostname -s', computer

   ;  Get the current date and time:
   dat = today()

   ;  Get the name of this routine:
   info = SCOPE_TRACEBACK(/STRUCTURE)
   rout_name = info[N_ELEMENTS(info) - 1].ROUTINE

   ;  Save the results in the output file:
   a_name = 'MISR_L1B2_GM_Data_Avail_' + computer + '_' + dat + '.txt'
   a_spec = a_dir + a_name
   fmt = '(A20, A)'

   ;  Open the output file:
   OPENW, ounit, a_spec, /GET_LUN
   PRINTF, ounit, 'File name: ', FILE_BASENAME(a_spec), FORMAT = fmt
   PRINTF, ounit, 'File path: ', FILE_DIRNAME(a_spec, /MARK_DIRECTORY), $
      FORMAT = fmt
   PRINTF, ounit
   PRINTF, ounit, 'Generated by: ', rout_name, FORMAT = fmt
   PRINTF, ounit, 'Saved on: ', dat, FORMAT = fmt
   PRINTF, ounit
   PRINTF, ounit, 'Content: ', 'MISR L1B2 data availability on computer ' + $
      computer + '.', FORMAT = fmt
   PRINTF, ounit

   n_dirs = misr_data.NumDirs

   FOR j = 0, n_dirs - 1 DO BEGIN
      PRINTF, ounit, misr_data.DirNames[j], misr_data.DirSizes[j], $
         'from: ' + misr_data.IniDate[j] + ' to: ' + misr_data.FinDate[j], $
         FORMAT = '(3X, A, 3X, A, 3X, A)'
   ENDFOR
   CLOSE, ounit
   PRINT, 'The output file'
   PRINT, FILE_BASENAME(a_spec)
   PRINT, 'has been created in the folder'
   PRINT, FILE_DIRNAME(a_spec)

END
